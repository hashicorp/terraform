---
page_title: Import - Configuration Language
description: >-
  Import and manage existing resources with Terraform using configuration-driven import.
---

# Import

-> **Note:** Import blocks are only available in Terraform v1.5.0 and later.

~> **Experimental:** Import blocks are an experimental Terraform feature. While we do not expect to make backwards-incompatible changes to syntax, the processing of imports during the plan stage, generating configuration, and the `-generate-config-out` flag may change in future releases.

The `import` block can be used to import existing infrastructure resources into Terraform, bringing them under Terraform's management. Once imported, an existing resource is present in Terraform's configuration and state, much like any other resource Terraform created. You can then manage that resource like any other, updating its attributes and destroying it as part of a standard resource lifecycle.
The `import` block records that Terraform imported the resource and that Terraform did not previously create that resource with its configuration. Put another way, the `import` block records the user's intent to add an import to Terraform's plan graph.

## Syntax

The `import` block can appear in any Terraform configuration file. A common pattern is to create an `imports.tf` file, or to place each `import` block beside the `resource` block it imports into.

```hcl
import {
  to = aws_instance.example
  id = "i-abcd1234"
}

resource "aws_instance" "example" {
  name = "hashi"
  # (other resource arguments...)
}
```

The above `import` block defines an import of the AWS instance with the ID "i-abcd1234" into the `aws_instance.example` resource in the root module.

The `import` block has the following arguments:
 - `to` - The resource instance address this import is targeting.
 - `id` - A string with the import ID of the resource.
 - `provider` (optional) - An optional custom resource provider, see [The Resource provider Meta-Argument](/terraform/language/meta-arguments/resource-provider) for details.

## Plan and Apply an Import

Terraform processes the `import` block during the plan stage. Along with create, update, remove, and move, import is an action Terraform performs on a resource. Once a plan is approved, Terraform imports the resource into its state during the subsequent apply stage.

You can use `import` blocks by doing the following:
1. Define an `import` block for the resource(s).
1. Add a `resource` block in your configuration.
1. Run `terraform plan` to see how Terraform plans to import the resource(s).
1. Apply the configuration and have Terraform import resources and update your Terraform state.

> **Hands-on:** Try the [State Import](/terraform/tutorials/state/state-import?utm_source=WEBSITE&utm_medium=WEB_IO&utm_offer=ARTICLE_PAGE&utm_content=DOCS) tutorial.

```hcl
provider "aws" {
  region = "eu-west-1"
}

import {
  to = aws_iot_thing.bar
  id = "foo"
}
```

### 2. Add the `resource` block to configuration

```hcl
resource aws_iot_thing "bar" {
  name = "foo"
}
```

### 3. Plan the import
Run `terraform plan`. You should see output like the following:


```
$ terraform plan

aws_iot_thing.bar: Preparing import... [id=foo]
aws_iot_thing.bar: Refreshing state... [id=foo]

Terraform will perform the following actions:

  # aws_iot_thing.bar will be imported
    resource "aws_iot_thing" "bar" {
        arn               = "arn:aws:iot:eu-west-1:1234567890:thing/foo"
        attributes        = {}
        default_client_id = "foo"
        id                = "foo"
        name              = "foo"
        version           = 1
    }

Plan: 1 to import, 0 to add, 0 to change, 0 to destroy.
```

### 4. Apply the import
Run `terraform apply`. You should see output like the following:

```
$ terraform apply

aws_iot_thing.bar: Preparing import... [id=foo]
aws_iot_thing.bar: Refreshing state... [id=foo]

Terraform will perform the following actions:

  # aws_iot_thing.bar will be imported
    resource "aws_iot_thing" "bar" {
        arn               = "arn:aws:iot:eu-west-1:1234567890:thing/foo"
        attributes        = {}
        default_client_id = "foo"
        id                = "foo"
        name              = "foo"
        version           = 1
    }

Plan: 1 to import, 0 to add, 0 to change, 0 to destroy.
aws_iot_thing.bar: Importing... [id=foo]
aws_iot_thing.bar: Import complete [id=foo]

Apply complete! Resources: 1 imported, 0 added, 0 changed, 0 destroyed.

```

## Resource Configuration

When Terraform imports a resource, it needs you to define that resource in configuration before saving it to state. You can do this by either writing a `resource` block or by generating configuration. 

### Write a `resource` block

Write a `resource` block for the resource you are importing, whose address matches the import block target. For example:

```hcl
import {
  to = aws_instance.example
  id = "i-abcd1234"
}

resource "aws_instance" "example" {
  name = "renderer"
}
```

### Option: Generate configuration

Terraform can generate HCL for resources which do not already exist in configuration. The HCL produced by Terraform is intended to act as a template, and contains Terraform's best guess at the appropriate value for each resource argument. You may find that you arrive at an ideal configuration by starting from the HCL generated by Terraform, removing some attributes, adjusting the value of others, and rearranging `resource` blocks into files and modules as appropriate.

To instruct Terraform to generate configuration during the plan, supply a file path with `-generate-config-out`. The file should not already exist. For example:

```
terraform plan -generate-config-out=generated_resources.tf
```

Terraform will generate configuration for any resources targeted by an `import` block that do not already exist in configuration, and write the HCL to `generated_resources.tf`.

Please see the [Generating Configuration](/terraform/language/import/generating-configuration) page for more details on config generation.

## Examples

### Import into module

```hcl
import {
  to = module.instances.aws_instance.example
  id = "i-abcd1234"
}
```

### Import into resource configured with `count`

```hcl
import {
  to = aws_instance.example[0]
  id = "i-abcd1234"
}
```


### Import into resource configured with `for_each`
```hcl
import {
  to = aws_instance.example["foo"]
  id = "i-abcd1234"
}
```

### Import with custom resource provider

```hcl
provider "aws" {
  alias = "europe"
  region = "eu-west-1"
}

import {
  provider = aws.europe
  to = aws_instance.example["foo"]
  id = "i-abcd1234"
}
```
