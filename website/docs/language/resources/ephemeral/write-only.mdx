---
page_title: Use write-only arguments
description: Learn how to use write-only arguments to set temporary values on resources that are not stored in Terraform's state or plan files.
---

# Use write-only arguments

Terraform resources can include ephemeral arguments for data that only needs to exist temporarily. An ephemeral argument on a resource is called a **write-only argument**. Use write-only arguments to store sensitive data for the current Terraform operation, such as a short-lived password, token, or session identifier. 

## Background

Write-only arguments are only available during the runtime of a Terraform operation, and Terraform omits those values from state and plan files.

You can use write-only arguments to pass ephemeral data directly to a resource without Terraform persisting that value. For example, you can directly pass a generated password or a fetched token to a write-only argument without risking Terraform exposing that value in its artifacts.

Write-only arguments are unique among other ephemeral constructs in Terraform because they accept both ephemeral and non-ephemeral values.

## Requirements

To define write-only arguments you must use Terraform v.1.11 or later, and use  a `resource` that supports write-only arguments.

## Declare a write-only argument

Providers indicate in the Terraform registry whether a resource contains write-only arguments. For example, the `aws` provider's `aws_db_instance` resource has a write-only `password_wo` argument. The `password_wo` argument accepts a value to use as the database password:

```hcl
ephemeral "random" "password" {
  length = 16
}

resource "aws_db_instance" "test" {
  instance_class      = "db.t5.micro"
  allocated_storage   = "5"
  engine              = "postgres"
  username            = "admin"
  skip_final_snapshot = true
  password_wo = <ephemeral or non-ephemeral value>
}
```

Write-only arguments can accept both ephemeral and non-ephemeral values. For example, you could use a literal string as the value of a write-only argument:

```hcl
resource "aws_db_instance" "test" {
  instance_class      = "db.t5.micro"
  allocated_storage   = "5"
  engine              = "postgres"
  username            = "admin"
  skip_final_snapshot = true
  password_wo = "your-password-here"
}
```

However, write-only arguments are most useful when you use ephemeral values. For example, you can use an `ephemeral` resource to generate a random password and pass it to the `password_wo` write-only argument:

```hcl
ephemeral "random" "password" {
  length = 16
}

resource "aws_db_instance" "test" {
  instance_class      = "db.t5.micro"
  allocated_storage   = "5"
  engine              = "postgres"
  username            = "admin"
  skip_final_snapshot = true
  password_wo = ephemeral.random.password.value
}
```

During a Terraform operation, the provider uses the `password_wo` value to configure the database instance and then discards that value without storing it in the plan or state file. 

Terraform sends write-only arguments to the provider every time it needs to create or update that argument's resource in your configuration. Meaning, every time an apply creates or updates the `aws_db_instance` resource, Terraform sends the `password_wo` argument to the `aws` provider. The provider uses the `password_wo` value to configure the database instance and then discards the password value afterwards.

## Control updates with version attributes

Write-only arguments often have associated version arguments that control when Terraform updates the write-only argument. The version attribute is stored in state, allowing Terraform to track when the write-only argument changes.

For example, the `aws_db_instance` resource has a `password_wo_version` argument that controls when Terraform sends the `password_wo` value to the provider. The provider only receives the `password_wo` value when the `password_wo_version` argument changes or when creating a new resource:

```hcl
resource "aws_db_instance" "main" {
  instance_class      = "db.t5.micro"
  allocated_storage   = "5"
  engine             = "postgres"
  username           = "admin"
  password_wo         = "new-password-here"
  password_wo_version = 2
}
```

To trigger an update of a write-only argument, modify the write-only argument's value in your configuration and increment the corresponding version attribute.



